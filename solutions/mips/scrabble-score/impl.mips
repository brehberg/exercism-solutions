# | Register | Usage     | Type    | Description                  |
# | -------- | --------- | ------- | ---------------------------- |
# | `$a0`    | input     | address | null-terminated input string |
# | `$v0`    | output    | integer | scrabble score               |
# | `$t0-9`  | temporary | any     | for temporary storage        |

.globl scrabble_score

.data

values: .byte 1, 3, 3, 2, 1, 4, 2, 4, 1, 8, 5, 1, 3, 
              1, 1, 3, 10, 1, 1, 1, 1, 4, 4, 8, 4, 10

.text

scrabble_score:
        move    $v0, $zero
        li      $t1, 0x20
        li      $t2, 'a'
        li      $t3, 'z'

loop:
        lb      $t0, 0($a0)      # load character
        addi    $a0, $a0, 1      # move pointer
        or      $t0, $t0, $t1    # to lower case
        blt     $t0, $t2, exit   # exit if < a
        bgt     $t0, $t3, exit   # exit if > z
        sub     $t4, $t0, $t2    # letter index
        lb      $t5, values($t4) # letter value
        add     $v0, $v0, $t5    # add value
        b       loop

exit:
        jr $ra